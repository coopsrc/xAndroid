if (project.ext.has("xandroidPublishEnabled") && project.ext.xandroidPublishEnabled) {
    apply plugin: 'bintray-release'
    publish {
        artifactId = releaseArtifact
        desc = releaseDescription
        publishVersion = versionName
        repoName = getBintrayRepo()
        userOrg = 'coopsrc'
        groupId = 'com.coopsrc.xandroid'
        website = 'https://github.com/coopsrc/xAndroid'
        bintrayUser = System.getenv('BINTRAY_USER')
        bintrayKey = System.getenv('BINTRAY_KEY')
    }

    gradle.taskGraph.whenReady { taskGraph ->
        project.tasks.findAll {
            task -> task.name.contains("generatePomFileFor")
        }.forEach { task ->
            task.doLast {
                task.outputs.files.filter {
                    File file ->
                        file.path.contains("publications") && file.name.matches("^pom-.+\\.xml\$")
                }.forEach {
                    File file -> addLicense(file)
                }
            }
        }
    }
}

def getBintrayRepo() {
    boolean publicRepo = hasProperty('publicRepo') && property('publicRepo').toBoolean()
    return publicRepo ? 'xAndroid' : 'xAndroid'
}

static void addLicense(File pom) {
    def licenseNode = new Node(null, "license")
    licenseNode.append(new Node(null, "name", "The MIT License (MIT)"))
    licenseNode.append(new Node(null, "url", "https://coopsrc.mit-license.org/2015/license.txt"))
    licenseNode.append(new Node(null, "distribution", "repo"))
    def licensesNode = new Node(null, "licenses")
    licensesNode.append(licenseNode)

    def xml = new XmlParser().parse(pom)
    xml.append(licensesNode)

    def writer = new PrintWriter(new FileWriter(pom))
    writer.write("<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n")
    def printer = new XmlNodePrinter(writer)
    printer.preserveWhitespace = true
    printer.print(xml)
    writer.close()
}